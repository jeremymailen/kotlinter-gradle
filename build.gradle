plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.0'
    id 'com.gradle.plugin-publish' version '0.9.8'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'idea'
}

repositories {
    jcenter()
    maven { url 'https://maven.google.com' }
}

dependencies {
    compile 'com.github.shyiko:ktlint:0.13.0'
    compileOnly 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    compileOnly 'com.android.tools.build:gradle:2.3.3'
    testCompile 'junit:junit:4.12'
    testRuntime 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    testRuntime 'com.android.tools.build:gradle:2.3.3'
}

version = '1.6.0'
group = 'org.jmailen.gradle'
def pluginId = 'org.jmailen.kotlinter'

gradlePlugin {
    plugins {
        kotlinterPlugin {
            id = pluginId
            implementationClass = 'org.jmailen.gradle.kotlinter.KotlinterPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/jeremymailen/kotlinter-gradle'
    vcsUrl = 'https://github.com/jeremymailen/kotlinter-gradle'
    tags = ['kotlin', 'ktlint', 'lint', 'format', 'style', 'android']

    plugins {
        kotlinterPlugin {
            id = pluginId
            displayName = 'Kotlin Lint plugin'
            description = 'Lint and formatting for Kotlin using ktlint with configuration-free setup on JVM and Android projects'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}
