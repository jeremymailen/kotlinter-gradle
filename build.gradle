plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.41'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'idea'
}

repositories {
    jcenter()
    maven { url 'https://maven.google.com' }
}

configurations {
    testkit
}

dependencies {
    compile 'com.github.shyiko:ktlint:0.23.1'
    compile 'me.cassiano:ktlint-html-reporter:0.1.2'
    compileOnly 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    compileOnly 'com.android.tools.build:gradle:3.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.9.1'
    testCompile 'com.mkobit.gradle.test:assertj-gradle:0.2.0'
    testCompile 'com.mkobit.gradle.test:gradle-test-kotlin-extensions:0.3.0'
    testRuntime 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    testRuntime 'com.android.tools.build:gradle:3.0.1'

    testkit 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    testkit 'com.android.tools.build:gradle:3.0.1'
}

pluginUnderTestMetadata {
    pluginClasspath = sourceSets.main.runtimeClasspath + configurations.testkit
}

version = '1.12.0'
group = 'org.jmailen.gradle'
def pluginId = 'org.jmailen.kotlinter'

gradlePlugin {
    plugins {
        kotlinterPlugin {
            id = pluginId
            implementationClass = 'org.jmailen.gradle.kotlinter.KotlinterPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/jeremymailen/kotlinter-gradle'
    vcsUrl = 'https://github.com/jeremymailen/kotlinter-gradle'
    tags = ['kotlin', 'ktlint', 'lint', 'format', 'style', 'android']

    plugins {
        kotlinterPlugin {
            id = pluginId
            displayName = 'Kotlin Lint plugin'
            description = 'Lint and formatting for Kotlin using ktlint with configuration-free setup on JVM and Android projects'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}
