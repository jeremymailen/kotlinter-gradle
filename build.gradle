plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jmailen.kotlinter' version '2.1.3'
    id 'idea'
}

repositories {
    jcenter()
    google()
}

def ktlintVers = '0.36.0'

dependencies {
    implementation "com.pinterest.ktlint:ktlint-core:$ktlintVers"
    implementation "com.pinterest.ktlint:ktlint-reporter-checkstyle:$ktlintVers"
    implementation "com.pinterest.ktlint:ktlint-reporter-json:$ktlintVers"
    implementation "com.pinterest.ktlint:ktlint-reporter-html:$ktlintVers"
    implementation "com.pinterest.ktlint:ktlint-reporter-plain:$ktlintVers"
    implementation "com.pinterest.ktlint:ktlint-ruleset-experimental:$ktlintVers"
    implementation "com.pinterest.ktlint:ktlint-ruleset-standard:$ktlintVers"

    compileOnly 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    compileOnly 'com.android.tools.build:gradle:3.5.3'

    testImplementation 'junit:junit:4.13'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.jetbrains:annotations:18.0.0'
}

// Required to put all `compileOnly` dependencies on the classpath for the functional test suite
tasks.withType(PluginUnderTestMetadata).configureEach {
    pluginClasspath.from(configurations.compileOnly)
}

version = '2.3.0'
group = 'org.jmailen.gradle'
def pluginId = 'org.jmailen.kotlinter'

gradlePlugin {
    plugins {
        kotlinterPlugin {
            id = pluginId
            implementationClass = 'org.jmailen.gradle.kotlinter.KotlinterPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/jeremymailen/kotlinter-gradle'
    vcsUrl = 'https://github.com/jeremymailen/kotlinter-gradle'
    tags = ['kotlin', 'ktlint', 'lint', 'format', 'style', 'android']

    plugins {
        kotlinterPlugin {
            id = pluginId
            displayName = 'Kotlin Lint plugin'
            description = 'Lint and formatting for Kotlin using ktlint with configuration-free setup on JVM and Android projects'
        }
    }
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

wrapper {
    gradleVersion = '6.0.1'
}
