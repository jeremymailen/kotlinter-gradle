plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jmailen.kotlinter' version '2.0.0'
    id 'idea'
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://maven.google.com' }
}

dependencies {
    implementation 'com.pinterest:ktlint:0.35.0'
    implementation 'me.cassiano:ktlint-html-reporter:0.2.1'
    
    compileOnly 'org.jetbrains.kotlin:kotlin-gradle-plugin'
    compileOnly 'com.android.tools.build:gradle:3.4.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
    testRuntime 'com.android.tools.build:gradle:3.4.2'
}

// Required to put the Kotlin plugin on the classpath for the functional test suite
tasks.withType(PluginUnderTestMetadata).configureEach {
    pluginClasspath.from(configurations.compileOnly)
}

version = '2.1.2'
group = 'org.jmailen.gradle'
def pluginId = 'org.jmailen.kotlinter'

gradlePlugin {
    plugins {
        kotlinterPlugin {
            id = pluginId
            implementationClass = 'org.jmailen.gradle.kotlinter.KotlinterPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/jeremymailen/kotlinter-gradle'
    vcsUrl = 'https://github.com/jeremymailen/kotlinter-gradle'
    tags = ['kotlin', 'ktlint', 'lint', 'format', 'style', 'android']

    plugins {
        kotlinterPlugin {
            id = pluginId
            displayName = 'Kotlin Lint plugin'
            description = 'Lint and formatting for Kotlin using ktlint with configuration-free setup on JVM and Android projects'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

wrapper {
    gradleVersion = '5.5.1'
}
